// 'Parser' rules

top_level = { SOI ~ entry* ~ EOI }

entry = { parse_rule | token_rule }

parse_rule = { rule_name ~ ":" ~ rule_body ~ ";" }

rule_body = { rule_piece ~ ("|" ~ rule_piece)* }

rule_piece = { (rule_name ~ "=")? ~ rule_part+ }

rule_part = { rule_elem ~ ("+" | "*" | "?")? | "[" ~ rule_body ~ "]" }

rule_elem = { "(" ~ rule_body ~ ")" | rule_name | token_name | token_lit }

token_rule = { token_name ~ ":" ~ token_lit ~ ";" }

// 'Lexer' rules

rule_name = @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }

token_name = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }

token_lit = @{ "'" ~ (!"'" ~ ANY)* ~ "'"}

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ "#" ~ ANY* ~ NEWLINE+ }
